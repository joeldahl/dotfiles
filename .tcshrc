#
# .tcshrc - tcsh configuration file
#
# Written by:
# Joel Dahl <joel@vnode.se>
#

alias h history
alias ls ls -ahoGF

umask 22

set path = (/sbin /bin /usr/sbin /usr/bin /usr/games /usr/local/sbin /usr/local/bin $HOME/bin)

if ($uid == 0) then
	set user = root
endif

if ($?prompt) then
	set prompt = "%n@%m [%~] "
	set color
	set filec
	set autolist
	set autoexpand
	set nobeep
	set rmstar
	set history = 5000
	set savehist = (5000 merge)
	set histdup = erase
	if ($?tcsh) then
		bindkey "^W" backward-delete-word
		bindkey -k up history-search-backward
		bindkey -k down history-search-forward
	endif
endif

if ( -X vim ) then
	setenv EDITOR vim
	alias vi vim
else
	setenv EDITOR vi
endif

setenv PAGER less
setenv BLOCKSIZE K
setenv LC_CTYPE sv_SE.UTF-8
setenv LSCOLORS "dxfxcxdxbxegedabagacad"
setenv LS_COLORS "di=33:ln=35:so=32:pi=33:ex=31:bd=34;46:cd=34;43:su=30;41:sg=30;46:tw=30;42:ow=30;43:"
setenv TERM xterm-256color

set UNAME = `uname`
if ("$UNAME" == "Darwin") then
	set mbAF = "1 1"
	set mdAF = "1 1"
	set soAF = "3 3"
	set soAB = "4 4"
	set usAF = "4 4"
else if ("$UNAME" == "FreeBSD") then
	set mbAF = "1"
	set mdAF = "1"
	set soAF = "3"
	set soAB = "4"
	set usAF = "4"
	complete man 'p/*/c/'
	complete pkg_{delete,info,version} 'n/-W/f/' 'n#-O#D:/usr/ports/#' 'C#*#D:/var/db/pkg/# #'
	complete portmaster 'n@*@`\ls -1 /var/db/pkg | sed "s%/var/db/pkg/%%;s%^\(.*\)-[0-9].*%\1%"`@'
	complete portsnap 'p/*/(fetch cron extract update)/'
	complete sysctl 'n/*/`sysctl -aN`/'
endif

if ($?prompt) then
	setenv LESS_TERMCAP_mb `echotc md; echotc AF $mbAF`
	setenv LESS_TERMCAP_md `echotc md; echotc AF $mdAF`
	setenv LESS_TERMCAP_me `echotc me`
	setenv LESS_TERMCAP_se `echotc me`
	setenv LESS_TERMCAP_so `echotc md; echotc AF $soAF; echotc AB $soAB`
	setenv LESS_TERMCAP_ue `echotc me`
	setenv LESS_TERMCAP_us `echotc md; echotc AF $usAF`
endif
